plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.openapi.generator' version '7.6.0'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.15.0'
}

group = 'com.bank'
version = '0.0.1-SNAPSHOT'
description = 'ms-clientes'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // RabbitMQ
    implementation 'org.springframework.boot:spring-boot-starter-amqp'

    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.5.0'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.22'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'org.springframework.boot:spring-boot-starter-validation'


    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'com.h2database:h2'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/resources/api/openapi.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "com.bank.clients.controller.api"
    modelPackage = "com.bank.clients.model.dto"
    configOptions = [
            interfaceOnly: "true",
            useSpringBoot3: "true",
            skipDefaultInterface: "true",
            useTags: "true",
            reactive: "true"
    ]
}

sourceSets {
    main {
        java { srcDir "$buildDir/generated/src/main/java" }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn tasks.named('test')
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/com/bank/clients/model/dto/**',
                    '**/com/bank/clients/controller/api/**',
                    '**/com/bank/clients/config/**',
                    '**/com/bank/clients/MsClientesApplication.class'
            ])
        }))
    }
}

pitest {
    junit5PluginVersion = '1.2.1'
    targetClasses = ['com.bank.clients.*']
    excludedClasses = [
            'com.bank.clients.config.*',
            'com.bank.clients.model.dto.*',
            'com.bank.clients.controller.api.*',
            'com.bank.clients.MsClientesApplication'
    ]
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = true
}

compileJava.dependsOn tasks.openApiGenerate

tasks.named('build') {
    dependsOn 'pitest'
}
